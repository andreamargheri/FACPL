	Policy doctor < permit-unless-deny  
		target:
			equal ( "medical doctor" , subject/role ) &&
			equal ( "TREATEMENT" , subject/purposeofuse ) &&
			equal ( "34133-9" , resource/resource-id )
		rules:
			Rule rule1 ( permit  
				target:
					equal ( "Read" , action/action-id ) 
				condition:
					subset ( bag("PRD-003","PRD-005"), 
						subject/hl7.permission)
				obl:
					[ permit  M  log ( system/system-id , "Prescription Read" ) ])
			Rule rule2 ( permit 
				target:
					equal ( "Write" , action/action-id ) 
				condition:
					subset ( bag("PRD-003","PRD-005","PRD-010","PRD-016"), 
						subject/hl7.permission)
				obl: 
					[ permit  M  log ( system/system-id , "Prescription Update" ) ]	)
		obl:
			[ deny M  log ( subject/doctor-id , resource/resource-id , action/action-id) ]
			[ permit O mailTo (resource/resource-id.email , "Your medical record has been requested by EpSOS subject")]
	 >
	 Policy pharmacist < permit-unless-deny  
		target:
			equal ( "pharmacist" , subject/role ) &&
			equal ( "TREATEMENT" , subject/purposeofuse ) &&
			equal ( "34133-9" , resource/resource-id )
		 rules:
			Rule rule1 ( permit  
				target:
					equal ( "Read" , action/action-id ) 
				condition:
					subset ( bag("PRD-003","PRD-005","PRD-010","PRD-016"), 
						subject/hl7.permission)
				obl:
					[ permit  M  log ( system/system-id , "Prescription Read" ) ])
			Rule rule2 ( deny
				target: 
					equal ("Write", action/action-id)
			) 
		obl:
		[ deny M  log ( subject/doctor-id , resource/resource-id , action/action-id) ]
		[ permit O mailTo (resource/resource-id.email , "Your medical record has been requested by EpSOS subject")]
	 >

