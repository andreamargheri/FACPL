
	PolicySet ePrescription { deny-unless-permit
		target: equal ( "34133-9" , resource/resource-id ) 
		policies:	
		Policy doctorPrescr < deny-unless-permit   
			target:
				equal ( "medical doctor" , subject/role ) &&
				(equal ( "TREATEMENT" , subject/purposeofuse ) || equal ("EMERGENCY", subject/purposeofuse))
			rules:
				Rule rule1 ( permit  
					target:
						equal ( "Read" , action/action-id ) 
					condition:
						subset ( bag("PRD-003","PRD-005"), subject/hl7.permission)
					obl:
						[ permit  M  log ( system/system-id , "ePrescription Read" ) ])
				Rule rule2 ( permit 
					target:
						equal ( "Write" , action/action-id ) 
					condition:
						and (equal(subject/doctor-id, resource/patient-id.doctor-id),
							subset ( bag("PRD-003","PRD-005","PRD-010","PRD-016"), subject/hl7.permission))
					obl: 
						[ permit  M  log ( system/system-id , "ePrescriptions Update" ) ]	)
		 >
		 Policy pharmacistPrescr < deny-unless-permit  
			target:
				equal ( "pharmacist" , subject/role ) &&
				(equal ( "TREATEMENT" , subject/purposeofuse ) || equal ("EMERGENCY", subject/purposeofuse))
			rules:
				Rule rule1 ( permit  
					target:
						equal ( "Read" , action/action-id ) 
					condition:
						subset ( bag("PRD-003","PRD-005","PRD-011"), subject/hl7.permission)
					obl:
						[ permit  M  log ( system/system-id , "ePrescription Read" ) ])
				Rule rule2 ( permit
					target: 
						equal ("Write", action/action-id) && equal ("EMERGENCY", subject/purposeofuse)
					condition:
						subset ( bag("PRD-003","PRD-005","PRD-011","PRD-017"), subject/hl7.permission)
					obl: 
						[ permit  M  log ( system/system-id , "ePrescriptions Update" ) ]) 
		 >	 
		 obl:
			[ deny M  log ( subject/doctor-id , resource/resource-id , action/action-id) ]
			[ permit O mail (resource/patient-id.email , "Your medical record has been requested by EpSOS subject")]
	 }
	 
	 PolicySet patientSummary { deny-unless-permit
	 	target: equal ("57829-4", resource/resource-id) 
 	 	policies: 
 	 	Policy doctorSummary < deny-unless-permit 
 	 			target: equal ( "medical doctor" , subject/role ) &&
				(equal ( "TREATEMENT" , subject/purposeofuse ) || equal ("EMERGENCY", subject/purposeofuse))  
 	 			rules: 
 	 	 			Rule rule1 (permit 
 	 	 				target: equal("Read", action/action-id)
 	 	 				condition:
						subset ( bag("PRD-006","PRD-007"), subject/hl7.permission)
					obl:
						[ permit  M  log ( system/system-id , "summary Read" ) ])
					Rule rule2 ( permit 
						target: equal ( "Write" , action/action-id ) 
						condition:
						and (equal(subject/doctor-id, resource/patient-id.doctor-id),
							subset ( bag("PRD-006","PRD-007","PRD-010","PRD-016"), subject/hl7.permission))
					obl: 
						[ permit  M  log ( system/system-id , "ePrescriptions Update" ) ]	) 
	 	> 
	 	Policy nurseSummary < deny-unless-permit
	 			target: equal("nurse", subject/role) &&	equal( "EMERGENCY", subject/purposeofuse)
	 			rules:
	 				Rule rule1 (permit
	 					target: equal("Read", action/action-id)
	 					obl:
						[ permit  M  log ( system/system-id , "summary Read", "nurse EMERGENCY") ]
	 				)
	 	>
	 	obl:
			[ deny M  log ( subject/doctor-id , resource/resource-id , action/action-id) ]
			[ permit O mail (resource/patient-id.email , "Your medical record has been requested by EpSOS subject")]
 	 }

